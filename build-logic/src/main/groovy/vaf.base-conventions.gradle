plugins {
    id "java-library"
    id "maven-publish"
    id "idea"
}

repositories {
    mavenCentral()
    maven {
        name = "ViaVersion"
        url "https://repo.viaversion.com"
    }
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    group = rootProject.maven_group
    version = rootProject.maven_version
    description = rootProject.maven_description
}

dependencies {
    compileOnly "com.viaversion:viaversion-common:5.0.4-SNAPSHOT"
    compileOnly "com.viaversion:viabackwards-common:5.0.4-SNAPSHOT"
}

publishing {
    repositories {
        maven {
            name = "Via"
            url = "https://repo.viaversion.com/"

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.name
            version = project.maven_version

            from components.java

            pom {
                name = "ViaAprilFools"
                description = project.description
                url = "https://github.com/ViaVersion/ViaAprilFools"
                licenses {
                    license {
                        name = "GPL-3.0 License"
                        url = "https://github.com/ViaVersion/ViaAprilFools/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "RK_01"
                    }
                    developer {
                        id = "FlorianMichael"
                        name = "EnZaXD"
                        email = "florian.michael07@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/ViaVersion/ViaAprilFools.git"
                    developerConnection = "scm:git:ssh://github.com/ViaVersion/ViaAprilFools.git"
                    url = "https://github.com/ViaVersion/ViaAprilFools.git"
                }
            }
        }
    }
}
